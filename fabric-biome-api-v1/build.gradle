archivesBaseName = "quilted_fabric_biome_api_v1"
version = getSubprojectVersion(project)
upstream_version = getSubprojectUpstreamVersion(project)

dependencies {
	modApi(getQslModule("core", "qsl_base"))
	modApi(getQslModule("worldgen", "biome"))
}

testDependencies(project, [
	':fabric-api-base',
	':fabric-resource-loader-v0',
	':fabric-registry-sync-v0',
	':fabric-data-generation-api-v1'
])

sourceSets {
	testmod {
		resources {
			srcDirs += [
				'src/testmod/generated'
			]
		}
	}
}

loom {
	runs {
		datagen {
			inherit testmodServer
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/testmod/generated")}"
			vmArg "-Dfabric-api.datagen.strict-validation"

			ideConfigGenerated = true
			runDir "build/datagen"
		}
	}
}

// Ensure the worldgen JSONs are re-created to ensure the data-gen still works
test.dependsOn runDatagen
runTestmodClient.dependsOn runDatagen
generateResources.dependsOn runDatagen
